advisorresources
| where type == "microsoft.advisor/recommendations"
| where properties.category == "HighAvailability"
| where properties.extendedProperties.recommendationSubCategory == "ServiceUpgradeAndRetirement"
| extend retirementFeatureName = properties.extendedProperties.retirementFeatureName
| extend retirementDate = properties.extendedProperties.retirementDate
| extend resourceId = tostring(properties.resourceMetadata.resourceId)
| extend shortDescription = properties.shortDescription.problem
| where retirementFeatureName != ''
| join kind=leftouter (
    resources
    | extend resourceId = tostring(id)
    | project 
        resourceId, 
        resourceLocation = location, 
        resourceTags = tags, 
        resourceType = type
) on resourceId
| join kind=leftouter (
    ResourceContainers 
    | where type=='microsoft.resources/subscriptions' 
    | project 
        subscriptionId,
        SubName=name
) on subscriptionId
| project 
    retirementFeatureName, 
    retirementDate, 
    resourceId, 
    resourceType, 
    shortDescription, 
    SubName, 
    location = iff(isempty(resourceLocation), "N/A", resourceLocation),
    tags = iff(isempty(resourceTags), "{}", tostring(resourceTags))
